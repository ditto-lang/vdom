module App exports (
    Next,
    mount,
)

import (js-task) Task (
    Task,
)

import (std) Maybe (
    Maybe(..),
)
import (std) Never (
    Never,
)
import (std) Result (
    Result(..),
)

import Element (
    Element,
)
import Html (
    Html,
)


type alias Next(state, event) = { 
    state: state,
    task: Maybe(Task(event, Never)),
}

type alias Args(state, event) = {
    container: Element,
    init: () -> Next(state, event),
    view: (state) -> Array(Html(event)),
    update: (state, event) -> Next(state, event),
}

mount = fn (args: Args(state, event)): Effect(Unit) -> mount_impl(
    fn (cb) ->
        let
            next = args.init()
        in
        match next.task with
        | Nothing -> do {
            return next.state
        }
        | Just(task) -> do {
            Task.fork(
                task,
                fn (result) ->
                    match result with
                    | Ok(event) -> cb(event)
                    | Err(nah) -> Never.unreachable(nah)
                    end,
            );
            return next.state
        }
        end,
    args.container,
    args.view,
    fn (state, event, cb) ->
        let
            next = args.update(state, event)
        in
        match next.task with
        | Nothing -> do {
            return next.state
        }
        | Just(task) -> do {
            Task.fork(
                task,
                fn (result) ->
                    match result with
                    | Ok(event) -> cb(event)
                    | Err(nah) -> Never.unreachable(nah)
                    end,
            );
            return next.state
        }
        end,
)

foreign mount_impl: (
    ((event) -> Effect(Unit)) -> Effect(state),
    Element,
    (state) -> Array(Html(event)),
    (state, event, (event) -> Effect(Unit)) -> Effect(state),
) -> Effect(Unit)
